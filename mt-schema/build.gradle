configurations {
    jaxb
}

task genJaxb {
    ext.sourcesDir = "${buildDir}/xsd_gen"
    ext.classesDir = "${buildDir}/classes"
    ext.schema = "src/main/schema/commonElements.xsd"

    outputs.dir classesDir

    doLast() {
        project.ant {
            taskdef name: "xjc", classname: "com.sun.tools.xjc.XJCTask", classpath: configurations.jaxb.asPath
            mkdir(dir: sourcesDir)
            mkdir(dir: classesDir)

            xjc(destdir: sourcesDir, schema: schema) {
                arg(value: "-wsdl")
                produces(dir: sourcesDir, includes: "**/*.java")
            }

            javac(destdir: classesDir, source: 1.6, target: 1.6, debug: true,
                    debugLevel: "lines,vars,source",
                    classpath: configurations.jaxb.asPath) {
                src(path: sourcesDir)
                include(name: "**/*.java")
                include(name: "*.java")
            }

            copy(todir: classesDir) {
                fileset(dir: sourcesDir, erroronmissingdir: false) {
                    exclude(name: "**/*.java")
                }
            }
        }
    }
}

dependencies {
    compile("wsdl4j:wsdl4j:$wsdl4jVersion")

    jaxb("org.glassfish.jaxb:jaxb-xjc:$jaxbXjcVersion")
    compile(files(genJaxb.classesDir).builtBy(genJaxb))
}